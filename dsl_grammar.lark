?start: strategy

strategy: entry_block? exit_block?

entry_block: ENTRY logic_expr
exit_block: EXIT logic_expr

ENTRY: "ENTRY:"
EXIT: "EXIT:"

?logic_expr: logic_expr AND logic_term   -> and_expr
           | logic_expr OR logic_term    -> or_expr
           | logic_term

?logic_term: comparison
           | cross_expr
           | "(" logic_expr ")"

comparison: value comparator value

cross_expr: prefix_cross | infix_cross

prefix_cross: CROSS_OP "(" value "," value ")"
infix_cross: value CROSS_OP value

CROSS_OP: "CROSS_ABOVE" | "CROSS_BELOW"

comparator: GT | LT | GTE | LTE | EQ
GT: ">"
LT: "<"
GTE: ">="
LTE: "<="
EQ: "=="

AND: "AND"
OR: "OR"

?value: value "+" atom   -> add
      | value "-" atom   -> sub
      | atom

?atom: atom "*" factor   -> mul
     | atom "/" factor   -> div
     | factor

?factor: function_call
       | IDENTIFIER
       | NUMBER
       | "(" value ")"

function_call: IDENTIFIER "(" arg_list ")"
arg_list: value ("," value)*

IDENTIFIER: /[a-zA-Z_][a-zA-Z0-9_]*/
NUMBER: /[0-9]+(\.[0-9]+)?/


%ignore " "
%ignore /\r?\n/

